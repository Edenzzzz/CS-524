{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 1-2\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "status = MathOptInterface.OPTIMAL\n",
      "Min time to complete: 46.0 min\n",
      "\n",
      "Job → Station assignments:\n",
      "  Job 1 → Station 3\n",
      "  Job 2 → Station 3\n",
      "  Job 3 → Station 3\n",
      "  Job 4 → Station 4\n",
      "  Job 5 → Station 4\n",
      "  Job 6 → Station 1\n",
      "  Job 7 → Station 4\n",
      "  Job 8 → Station 1\n",
      "\n",
      "Station workloads (including setup):\n",
      "  Station 1: 30.0 min (used=true)\n",
      "  Station 2: 0.0 min (used=false)\n",
      "  Station 3: 46.0 min (used=true)\n",
      "  Station 4: 45.0 min (used=true)\n"
     ]
    }
   ],
   "source": [
    "using JuMP, HiGHS\n",
    "\n",
    "\n",
    "I = 1:4    # stations\n",
    "J = 1:8    # jobs: 1=(T1,S1),2=(T1,S2),3=(T1,S3),4=(T2,S1),5=(T2,S2),6=(T3,S1),7=(T3,S2),8=(T3,S3)\n",
    "\n",
    "p = [\n",
    "    # j =  1   2   3   4   5   6   7   8\n",
    "    # T1   T1  T1  T2  T2  T3  T3  T3\n",
    "    17  20  17  13  19  11  13   9;   # i=1\n",
    "    18  18  21  18  21  13   9  13;   # i=2\n",
    "    12  17   9  21  17   9   9  17;   # i=3\n",
    "    16  14  21  12  15  14  12  17;   # i=4\n",
    "]\n",
    "\n",
    "# availability a[i] and setup s[i]\n",
    "a = [41, 45, 50, 46]\n",
    "s = [10, 12,  8,  6]\n",
    "\n",
    "model = Model(HiGHS.Optimizer)\n",
    "\n",
    "@variable(model, z[j in J, i in I], Bin)   # z[j,i] = 1 if job j on station i\n",
    "@variable(model, x[i in I], Bin)           # x[i]=1 if station i is used\n",
    "@variable(model, C >= 0)                   # makespan\n",
    "\n",
    "@constraint(model, [j in J], sum(z[j,i] for i in I) == 1)\n",
    "\n",
    "@constraint(model, [i in I], z[1,i] == z[2,i])\n",
    "@constraint(model, [i in I], z[2,i] == z[3,i])\n",
    "\n",
    "@constraint(model, [i in I],\n",
    "    s[i]*x[i] + sum(p[i,j]*z[j,i] for j in J) <= a[i]\n",
    ")\n",
    "\n",
    "# Link usage\n",
    "@constraint(model, [j in J, i in I], z[j,i] <= x[i])\n",
    "\n",
    "# Makespan definition\n",
    "@constraint(model, [i in I],\n",
    "    s[i]*x[i] + sum(p[i,j]*z[j,i] for j in J) <= C\n",
    ")\n",
    "\n",
    "@objective(model, Min, C)\n",
    "\n",
    "set_silent(model)\n",
    "optimize!(model)\n",
    "\n",
    "status = termination_status(model)\n",
    "@show status\n",
    "println(\"Min time to complete: $(round(value(C); digits=2)) min\")\n",
    "\n",
    "println(\"\\nJob → Station assignments:\")\n",
    "for j in J, i in I\n",
    "    if value(z[j,i]) > 0\n",
    "        println(\"  Job $j → Station $i\")\n",
    "    end\n",
    "end\n",
    "\n",
    "println(\"\\nStation workloads (including setup):\")\n",
    "for i in I\n",
    "    load = s[i]*value(x[i]) + sum(p[i,j]*value(z[j,i]) for j in J)\n",
    "    println(\"  Station $i: $(round(load; digits=2)) min \", \n",
    "            \"(used=$(value(x[i])>0.5))\")\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 3-2\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Auror locations: [1, 4, 7]\n",
      "Covered districts: [1, 2, 3, 4, 5, 6, 7, 8]\n",
      "Total covered population (thousands): 295.0\n"
     ]
    }
   ],
   "source": [
    "using JuMP, GLPK\n",
    "\n",
    "I = 1:8 # districts\n",
    "p = [40, 30, 35, 20, 15, 50, 45, 60]                # populations (thousands)\n",
    "d = [\n",
    "    0  3  4  6  1  9  8 10;\n",
    "    3  0  5  4  8  6  1  9;\n",
    "    4  5  0  2  2  3  5  7;\n",
    "    6  4  2  0  3  2  5  4;\n",
    "    1  8  2  3  0  2  2  4;\n",
    "    9  6  3  2  2  0  3  2;\n",
    "    8  1  5  5  2  3  0  2;\n",
    "   10  9  7  4  4  2  2  0\n",
    "]\n",
    "n = [d[i,j] ≤ 2 ? 1 : 0 for i in I, j in I]         # reachable within 2 s\n",
    "\n",
    "# Model\n",
    "model = Model(GLPK.Optimizer)\n",
    "@variable(model, a[i in I], Bin)                   # auror at i?\n",
    "@variable(model, c[i in I], Bin)                   # i covered?\n",
    "\n",
    "@constraint(model, sum(a[j] for j in I) == 3)      # exactly 3 aurors\n",
    "@constraint(model, [i in I],\n",
    "    c[i] <= sum(n[i,j] * a[j] for j in I)           # coverage if ≤2 s away\n",
    ")\n",
    "\n",
    "@objective(model, Max, sum(p[i] * c[i] for i in I)) # maximize covered population\n",
    "\n",
    "optimize!(model)\n",
    "\n",
    "# Results\n",
    "println(\"Auror locations: \", [j for j in I if value(a[j]) > 0])\n",
    "println(\"Covered districts: \", [i for i in I if value(c[i]) > 0])\n",
    "println(\"Total covered population (thousands): \", objective_value(model))\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 4-3"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Rotation: 4\n",
      "Optimal cyclic order (any rotation is equivalent):\n",
      "2T1S → 3T1S → 3T3S → 1T3S → 1T1S → 1T2S → 2T2S → 3T2S\n",
      "\n",
      "Daily minutes\n",
      "  making time : 78\n",
      "  cleaning  time : 73.0\n",
      "  TOTAL      : 151.0\n"
     ]
    }
   ],
   "source": [
    "using JuMP, GLPK, NamedArrays\n",
    "\n",
    "clean_duration_matrix = [\n",
    "     0  5  9 20 14 14 10  8;\n",
    "    19  0 10 20  9  6 13  6;\n",
    "    12 13  0 18 19 20 20 11;\n",
    "    20 21 22  0 25 19 20 25;\n",
    "     7 11 18  9  0  9  9 15;\n",
    "    12 10 16  5 16  0  9  7;\n",
    "    14 16 11  5 14 15  0 11;\n",
    "    10 11  7 14  8 19 16  0\n",
    "]\n",
    "\n",
    "job_pairs = [\n",
    "    (:1,:1), (:1,:2), (:1,:3),\n",
    "    (:2,:1), (:2,:2),\n",
    "    (:3,:1), (:3,:2), (:3,:3)\n",
    "]\n",
    "\n",
    "duration_NA = NamedArray(clean_duration_matrix,\n",
    "                  (job_pairs, job_pairs), (\"job pair\",\"job pair\"))\n",
    "\n",
    "make_times = Dict(zip(job_pairs,[10, 13, 7, 12, 11, 8, 8, 9]))\n",
    "\n",
    "clean = Matrix(duration_NA)\n",
    "make  = [make_times[j] for j in job_pairs]\n",
    "names = [string(tp[1], \"T\", tp[2], \"S\") for tp in job_pairs]  # nicer labels\n",
    "\n",
    "n = length(names)\n",
    "\n",
    "m = Model(GLPK.Optimizer)\n",
    "\n",
    "@variable(m, x[1:n,1:n], Bin)\n",
    "@variable(m, u[1:n])\n",
    "\n",
    "# exactly one next_jobessor / predecessor for every job\n",
    "for i in 1:n\n",
    "    @constraint(m, x[i,i] == 0)\n",
    "    @constraint(m, sum(x[i,j] for j in 1:n) == 1)\n",
    "    @constraint(m, sum(x[j,i] for j in 1:n) == 1)\n",
    "end\n",
    "\n",
    "# MTZ (root at node 1 only to break symmetry)\n",
    "@constraint(m, u[1] == 0)\n",
    "for i in 2:n\n",
    "    @constraint(m, 1 <= u[i] <= n-1)\n",
    "end\n",
    "for i in 2:n, j in 2:n\n",
    "    if i != j\n",
    "        @constraint(m, u[i] - u[j] + (n-1)*x[i,j] ≤ n-2)\n",
    "    end\n",
    "end\n",
    "\n",
    "@objective(m, Min, sum(clean[i,j]*x[i,j] for i=1:n, j=1:n) + sum(make))\n",
    "\n",
    "optimize!(m)\n",
    "\n",
    "# recover the tour \n",
    "next_job = Dict(i => findfirst(j -> value(x[i,j]) > 0, 1:n) for i in 1:n)\n",
    "cycle = Vector{Int}()\n",
    "cur = 1\n",
    "for _ in 1:n\n",
    "    push!(cycle, cur)\n",
    "    cur = next_job[cur]\n",
    "end\n",
    "\n",
    "# rotate so the wrap-around cleaning arc is the shortest one\n",
    "wrap_costs = [clean[cycle[end - i], cycle[1 + i]] for i in 0:n-1]\n",
    "rot = findmin(wrap_costs)[2] - 1           # find the min edge from the previous day to the first job\n",
    "println(\"Rotation: $rot\")\n",
    "cycle = circshift(cycle, -rot)\n",
    "\n",
    "clean_time = sum(clean[i,j]*value(x[i,j]) for i=1:n, j=1:n)\n",
    "total_time = clean_time + sum(make)\n",
    "\n",
    "println(\"Optimal cyclic order (any rotation is equivalent):\")\n",
    "println(join(names[cycle], \" → \"))\n",
    "\n",
    "println(\"\\nDaily minutes\")\n",
    "println(\"  making time : \", sum(make))\n",
    "println(\"  cleaning time : \", clean_time)\n",
    "println(\"  TOTAL      : \", total_time)\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 5-2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "Car movements:\n",
      "Epic           →      WestTowne: 5.0 car(s)\n",
      "Hilldale       →       Overture: 2.0 car(s)\n",
      "Hilldale       →       CampRand: 1.0 car(s)\n",
      "Olbrich        →         Garver: 1.0 car(s)\n",
      "Arboretum      →         Garver: 6.0 car(s)\n",
      "PicnicPt       →      CapitolSq: 2.0 car(s)\n",
      "PicnicPt       →       Overture: 2.0 car(s)\n",
      "PicnicPt       →      Arboretum: 2.0 car(s)\n",
      "\n",
      "Total cost $1049.5322402600048\n"
     ]
    }
   ],
   "source": [
    "# ───────────  Uber relocation – multiple-choice formulation  ───────────\n",
    "using JuMP, GLPK\n",
    "\n",
    "loc  = [\"CapitolSq\",\"Epic\",\"Overture\",\"CampRand\",\"Hilldale\",\n",
    "        \"WestTowne\",\"Olbrich\",\"Arboretum\",\"PicnicPt\",\"Garver\"]\n",
    "coord = [(0,0),(20,20),(18,10),(30,12),(35,0),\n",
    "         (33,25),(4,27),(4,10),(11,0),(2,15)]\n",
    "supply  = [6,11,4,8,12,2,14,11,15,7]\n",
    "demand = [8,6,8,9,9,7,13,7,9,14]\n",
    "N = length(loc)\n",
    "\n",
    "# Euclidean distances (miles)\n",
    "dist = [sqrt((coord[i][1]-coord[j][1])^2 + (coord[i][2]-coord[j][2])^2)\n",
    "        for i in 1:N, j in 1:N]\n",
    "\n",
    "# breakpoints and piecewise parameters\n",
    "B = [0,2,5,8]                       # 0-2, 2-5, 5-8 cars\n",
    "Δ = diff(B)                         # [2,3,3]\n",
    "c = [5,3,1]                         # $/mile, slopes\n",
    "p = [0,4,14]                        # intercepts\n",
    "\n",
    "b = supply .- demand             \n",
    "\n",
    "mip = Model(GLPK.Optimizer)\n",
    "\n",
    "@variable(mip, 0 <= w[1:N,1:N,1:3] <= B[end])   # flow if tier chosen\n",
    "@variable(mip, z[1:N,1:N,1:3], Bin)             # tier-choice\n",
    "\n",
    "#   B_{s-1} z ≤ w ≤ B_s z\n",
    "for i in 1:N, j in 1:N, s in 1:3\n",
    "    @constraint(mip, w[i,j,s] >= B[s]*z[i,j,s])      # lower\n",
    "    @constraint(mip, w[i,j,s] <= B[s+1]*z[i,j,s])    # upper\n",
    "end\n",
    "# exactly one piece active \n",
    "@constraint(mip, [i=1:N, j=1:N], sum(z[i,j,s] for s=1:3) == 1)\n",
    "\n",
    "# meet demand at every location\n",
    "for i in 1:N\n",
    "    @constraint(mip,\n",
    "        sum(w[i,j,s] for j=1:N, s=1:3) -\n",
    "        sum(w[j,i,s] for j=1:N, s=1:3) == b[i])\n",
    "end\n",
    "# Actually no need to create another variable nij to represent movement, wij is enough\n",
    "@objective(mip, Min,\n",
    "    sum( (c[s]*w[i,j,s] + p[s]*z[i,j,s]) * dist[i,j]\n",
    "         for i=1:N, j=1:N, s=1:3) )\n",
    "\n",
    "optimize!(mip)\n",
    "\n",
    "# ── display results ───────────────────────────────────────────────────\n",
    "println(\"\\nCar movements:\")\n",
    "for i in 1:N, j in 1:N\n",
    "    moved = sum(value.(w[i,j,s]) for s=1:3)\n",
    "    if moved > 1e-6 && i != j\n",
    "        println(rpad(loc[i],14),\" → \",lpad(loc[j],14),\": \",\n",
    "                round(moved), \" car(s)\")\n",
    "    end\n",
    "end\n",
    "println(\"\\nTotal cost \\$\", objective_value(mip))\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 6-1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 38,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\tdrink\tcolour\tpet\tnation\tsmoke\n",
      "House 1\twater\tyellow\tcat\tNorwegian\tDunhill\n",
      "House 2\ttea\tblue\thorse\tDane\tBlends\n",
      "House 3\tmilk\tred\tbird\tBrit\tPallMall\n",
      "House 4\tcoffee\tgreen\tfish\tGerman\tPrince\n",
      "House 5\tbeer\twhite\tdog\tSwede\tBlueMaster\n",
      "Fish is kept in house #4 by the German.\n"
     ]
    }
   ],
   "source": [
    "using JuMP, HiGHS   # HiGHS = fast open-source MILP solver\n",
    "\n",
    "houses = 1:5\n",
    "\n",
    "colours      = [:red,:green,:white,:yellow,:blue]\n",
    "nations      = [:Brit,:Swede,:Dane,:Norwegian,:German]\n",
    "drinks       = [:tea,:coffee,:milk,:beer,:water]\n",
    "cigars       = [:PallMall,:Dunhill,:Blends,:BlueMaster,:Prince]\n",
    "pets         = [:dog,:bird,:cat,:horse,:fish]\n",
    "\n",
    "attributes = Dict(:colour=>colours, :nation=>nations, :drink=>drinks,\n",
    "            :smoke=>cigars, :pet=>pets)\n",
    "\n",
    "m = Model(HiGHS.Optimizer)\n",
    "\n",
    "@variable(m, x[a in keys(attributes), h in houses, v in attributes[a]], Bin)\n",
    "\n",
    "# Each house has only one attribute of each type\n",
    "for a in keys(attributes), h in houses\n",
    "    @constraint(m, sum(x[a,h,v] for v in attributes[a]) == 1)\n",
    "end\n",
    "for a in keys(attributes), v in attributes[a]\n",
    "    @constraint(m, sum(x[a,h,v] for h in houses) == 1)\n",
    "end\n",
    "\n",
    "# 1. The Brit live in red house\n",
    "@constraint(m, [h in houses], x[:nation,h,:Brit] - x[:colour,h,:red] == 0)\n",
    "# 2. Swede owns dogs\n",
    "@constraint(m, [h in houses], x[:nation,h,:Swede] - x[:pet,h,:dog] == 0)\n",
    "# 3. Dane drinks tea\n",
    "@constraint(m, [h in houses], x[:nation,h,:Dane] - x[:drink,h,:tea] == 0)\n",
    "# 4. green is one position to the left of white\n",
    "@constraint(m, [h in 1:4], x[:colour,h,:green] - x[:colour,h+1,:white] == 0)\n",
    "# 5. green house owner drinks coffee\n",
    "@constraint(m, [h in houses], x[:colour,h,:green] - x[:drink,h,:coffee] == 0)\n",
    "# 6. PallMall = birds\n",
    "@constraint(m, [h in houses], x[:smoke,h,:PallMall] - x[:pet,h,:bird] == 0)\n",
    "# 7. yellow = Dunhill\n",
    "@constraint(m, [h in houses], x[:colour,h,:yellow] - x[:smoke,h,:Dunhill] == 0)\n",
    "# 8. centre house (h=3) drinks milk\n",
    "@constraint(m, x[:drink,3,:milk] == 1)\n",
    "# 9. Norwegian in first house\n",
    "@constraint(m, x[:nation,1,:Norwegian] == 1)\n",
    "\n",
    "#10. Blends next to cats\n",
    "for h in houses\n",
    "    left  = h > 1  ? x[:pet,h-1,:cat] : 0\n",
    "    right = h < 5 ? x[:pet,h+1,:cat] : 0\n",
    "    @constraint(m, x[:smoke,h,:Blends] <= left + right) # if the house has blends, either left or right has cats\n",
    "end\n",
    "#11 horses next to Dunhill\n",
    "for h in houses\n",
    "    left  = h > 1  ? x[:smoke,h-1,:Dunhill] : 0\n",
    "    right = h < 5 ? x[:smoke,h+1,:Dunhill] : 0\n",
    "    @constraint(m, x[:pet,h,:horse] <= left + right)\n",
    "end\n",
    "#12 BlueMaster = beer\n",
    "@constraint(m, [h in houses], x[:smoke,h,:BlueMaster] - x[:drink,h,:beer] == 0)\n",
    "#13 German = Prince\n",
    "@constraint(m, [h in houses], x[:nation,h,:German] - x[:smoke,h,:Prince] == 0)\n",
    "#14 Norwegian next to blue\n",
    "@constraint(m, x[:nation,1,:Norwegian] <= x[:colour,2,:blue])\n",
    "for h in 2:4\n",
    "    @constraint(m, x[:nation,h,:Norwegian] <= x[:colour,h-1,:blue] + x[:colour,h+1,:blue])\n",
    "end\n",
    "@constraint(m, x[:nation,5,:Norwegian] <= x[:colour,4,:blue])\n",
    "#15 Blends neighbour drinks water\n",
    "for h in houses\n",
    "    left  = h > 1  ? x[:drink,h-1,:water] : 0\n",
    "    right = h < 5 ? x[:drink,h+1,:water] : 0\n",
    "    @constraint(m, x[:smoke,h,:Blends] <= left + right)\n",
    "end\n",
    "set_silent(m)\n",
    "optimize!(m)\n",
    "\n",
    "\n",
    "# print a table of the houses and attributes\n",
    "for a in keys(attributes)\n",
    "    # tabular\n",
    "    print(\"\\t$(a)\")\n",
    "end\n",
    "\n",
    "for h in houses\n",
    "    println()\n",
    "    print(\"House $h\")\n",
    "    for a in keys(attributes)\n",
    "        print(\"\\t$(first(v for v in attributes[a] if value(x[a,h,v]) > 0))\")\n",
    "    end\n",
    "end\n",
    "\n",
    "println()\n",
    "fish_house = first(h for h in houses if value(x[:pet,h,:fish]) > 0.5)\n",
    "owner      = first(n for n in nations if value(x[:nation,fish_house,n]) > 0.5)\n",
    "println(\"Fish is kept in house #$fish_house by the $owner.\")\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.3",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
