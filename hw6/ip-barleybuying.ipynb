{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, HiGHS\n",
    "\n",
    "demand = 800\n",
    "max_percent = 0.4\n",
    "suppliers = 1:3\n",
    "# Piecewise linear functions' parameters\n",
    "nsegments = 3\n",
    "slope = [\n",
    "    9.2 9   7;\n",
    "    9   8.5 8.3;\n",
    "    11  8.5 7.5\n",
    "]\n",
    "intercept = [\n",
    "    0 20  420;\n",
    "    0 25  75;\n",
    "    0 250 550\n",
    "]\n",
    "endpoint = [\n",
    "    0 100 200 800;\n",
    "    0 50  250 800;\n",
    "    0 100 300 800;\n",
    "]\n",
    "\n",
    "m = Model(HiGHS.Optimizer)\n",
    "\n",
    "@variable(m, purchase[suppliers] >= 0)\n",
    "@variable(m, cost[suppliers]) # t variables in math model\n",
    "# Using math model names for other piecewise variables\n",
    "@variable(m, y[suppliers,1:nsegments], Bin)\n",
    "@variable(m, w[suppliers,1:nsegments])\n",
    "\n",
    "@objective(m, Min, sum(cost[s] for s in suppliers))\n",
    "\n",
    "@constraint(m, y_sum_to_1[s in suppliers],\n",
    "    sum(y[s,i] for i in 1:nsegments) == 1)\n",
    "\n",
    "@constraint(m, w_sum_to_x[s in suppliers],\n",
    "    sum(w[s,i] for i in 1:nsegments) == purchase[s])\n",
    "\n",
    "@constraint(m, w_lower_bound[s in suppliers, i in 1:nsegments],\n",
    "    endpoint[s,i]y[s,i] <= w[s,i])\n",
    "\n",
    "@constraint(m, w_upper_bound[s in suppliers, i in 1:nsegments],\n",
    "    w[s,i] <= endpoint[s,i+1]y[s,i])\n",
    "\n",
    "@constraint(m, t_defn[s in suppliers],\n",
    "    cost[s] == \n",
    "    sum(slope[s,i]w[s,i] + intercept[s,i]y[s,i] \n",
    "                for i in 1:nsegments))\n",
    "\n",
    "@constraint(m, meet_demand,\n",
    "    sum(purchase[s] for s in suppliers) == demand)\n",
    "\n",
    "@constraint(m, max_from_one_suppliers[s in suppliers],\n",
    "    purchase[s] <= max_percent*demand)\n",
    "\n",
    "set_silent(m)\n",
    "\n",
    "optimize!(m)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "Total cost: $6995.0\n",
      "Where to buy things from:\n",
      "Buy 320.0 pounds from supplier 1 for $2660.0\n",
      "Piecewise variables:\n",
      "w[1,1] = -0.0 y[1,1] = 0.0\n",
      "w[1,2] = 0.0 y[1,2] = 0.0\n",
      "w[1,3] = 320.0 y[1,3] = 1.0\n",
      "Buy 160.0 pounds from supplier 2 for $1385.0\n",
      "Piecewise variables:\n",
      "w[2,1] = -0.0 y[2,1] = 0.0\n",
      "w[2,2] = 160.0 y[2,2] = 1.0\n",
      "w[2,3] = 0.0 y[2,3] = 0.0\n",
      "Buy 320.0 pounds from supplier 3 for $2950.0\n",
      "Piecewise variables:\n",
      "w[3,1] = -0.0 y[3,1] = 0.0\n",
      "w[3,2] = 0.0 y[3,2] = 0.0\n",
      "w[3,3] = 320.0 y[3,3] = 1.0\n"
     ]
    }
   ],
   "source": [
    "# Print solution in a different cell (it gets mixed in with solver output otherwise)\n",
    "println(\"\\nTotal cost: \\$\", \n",
    "    round(objective_value(m),digits=2))\n",
    "println(\"Where to buy things from:\")\n",
    "for s in suppliers\n",
    "    println(\"Buy \", \n",
    "        round(value(purchase[s]),digits=2), \n",
    "        \" pounds from supplier \", \n",
    "        s, \" for \\$\", \n",
    "        round(value(cost[s]),digits=2))\n",
    "    println(\"Piecewise variables:\")\n",
    "    for i in 1:nsegments\n",
    "        println(\"w[\", s, \",\", i, \"] = \", \n",
    "            round(value(w[s,i]),digits=2), \n",
    "            \" y[\", s, \",\", i, \"] = \", \n",
    "            round(value(y[s,i]),digits=2))\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.3",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
